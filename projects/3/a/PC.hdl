// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/3/a/PC.hdl
/**
 * A 16-bit counter.
 * if      reset(t): out(t+1) = 0
 * else if load(t):  out(t+1) = in(t)
 * else if inc(t):   out(t+1) = out(t) + 1
 * else              out(t+1) = out(t)
 */
CHIP PC {
    IN in[16],inc, load, reset;
    OUT out[16];
    
    PARTS:
    Inc16(in=current, out=nextIfInc);
    Mux16(a=current, b=nextIfInc, sel=inc, out=nextIfNotload);
    Mux16(a=nextIfNotload, b=in, sel=load, out=nextIfNotreset);
    Mux16(a=nextIfNotreset, b=false, sel=reset, out=next);
    Register(in=next, load=true, out=current, out=out);
}
